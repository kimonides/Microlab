IN 20H 
CALL START_LED ;IF MSB=1 THEN SET LEDS TO START FROM MSB ELSE SET LEDS TO START FROM LSB
CALL CHECK_LSB ;IF LSB==0 WAIT
ANI 80H ;GET ONLY THE INPUT'S MSB
CPI 80H ; CHECK IF MSB==1 OR 0
MVI D,80H ;SET DIRECTION
MVI C,7FH ;SET LED STARTING POSITION SO IT ROTATES FROM MSB TO LSB FIRST
JZ GO_RIGHT ;IF MSB==1 ROTATE RIGHT
MVI D,01H ;SET DIRECTION
MVI C,FEH ; SET LED STARTING POSITION SO IT ROTATES FROM LSB TO MSB FIRST
JMP GO_LEFT ;ELSE IF MSB==0 ROTATE LEFT



GO_RIGHT:
MOV A,C ;C KEEPS THE LED'S VALUE,MOVE IT TO A SO WE CAN SET THE LEDS
OUT 30H ;SET THE LEDS
CALL DELAY ; 0,5s delay
IN 20H ;CHECK INPUT
CALL CHECK_LSB ;IF LSB==1 WAIT ELSE CONTINUE
ANI 80H ; GET ONLY THE INPUTS MSB
CMP D ;CHECK IF A HAS CHANGED VALUE SINCE LAST TIME,IF THE VALUE HAS CHANGED WE WILL CHANGE DIRECTION
JZ KEEP_GOING_RIGHT ;IF MSB HASN'T CHANGED IT'S VALUE KEEP GOING WITH THE CURRENT DIRECTION ELSE CHANGE
MOV D,A
JMP GO_LEFT
KEEP_GOING_RIGHT:
MOV A,C ;C KEEPS THE LED'S VALUE
CPI FEH ;CHECK IF AT BOUNDARY
JZ GO_LEFT ;IF AT BOUNDARY GO LEFT
RRC ;SHIFT THE LED
MOV C,A ;MOVE THE LED'S VALUE BACK TO C
JMP GO_RIGHT ;LOOP




GO_LEFT:
MOV A,C ;C KEEPS THE LED'S VALUE,MOVE IT TO A SO WE CAN SET THE LEDS
OUT 30H ;SET THE LEDS
CALL DELAY ; 0,5s delay
IN 20H ;CHECK INPUT
CALL CHECK_LSB ;IF LSB==1 WAIT ELSE CONTINUE
ANI 80H ; GET ONLY THE INPUTS MSB
CMP D ;CHECK IF A HAS CHANGED VALUE SINCE LAST TIME,IF THE VALUE HAS CHANGED WE WILL CHANGE DIRECTION
JZ KEEP_GOING_LEFT ;IF MSB HASN'T CHANGED IT'S VALUE KEEP GOING WITH THE CURRENT DIRECTION ELSE CHANGE
MOV D,A ;SINCE WE DIDNT JUMP BEFORE D!=A SO WE WILL CHANGE DIRECTION AND SET D'S NEW VALUE TO A'S MSB
JMP GO_RIGHT
KEEP_GOING_LEFT:
MOV A,C ;C KEEPS THE LED'S VALUE
CPI 7FH ;CHECK IF AT BOUNDARY
JZ GO_RIGHT ;IF AT BOUNDARY GO RIGHT
RLC ;SHIFT THE LED
MOV C,A ;MOVE THE LED'S VALUE BACK TO C
JMP GO_LEFT ;LOOP


CHECK_LSB:
IN 20H
MOV B,A ; B=KEEP A'S VALUE TEMPORARY
ANI 01H
CPI 01H ; LSB==1?
JNZ CHECK_LSB
MOV A,B ; RETURN A IT'S VALUE
RET

DELAY:
MOV E,C
LXI B,01F4H ;500MS
CALL DELB
MOV C,E
RET

START_LED:
MOV B,A ; B=KEEPS A VALUE 
ANI 80H ; GET THE INPUTS MSB
CPI 80H ; CHECK IF MSB==1
JNZ IF_0 ;IF MSB==0 THEN JUMP TO IF_0 WHERE WE WILL SET THE LEDS TO START FROM THE LSB
MVI A,7FH ; ELSE SET THE LEDS TO START FROM TEH MSB
OUT 30H
MOV A,B ;RETURN A'S VALUE
RET ;RETURN
IF_0:
MVI A,FEH ;ELSE SET THE LED'S VALUE TO START FROM THE LSB
OUT 30H
MOV A,B
RET



END